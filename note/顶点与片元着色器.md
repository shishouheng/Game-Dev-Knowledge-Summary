# 一、顶点片元着色器与表面着色器的区别

表面着色器是Unity特有的一种着色器代码类型，本质也是顶点片元着色器，使用类似表面材质的描述方式来定义光照、反射、透明度等属性。表面着色器封装了常用的光照模型和材质属性，使得开发更为简单，但缺点是灵活性低，无法控制渲染的细节

而顶点片元着色器更为复杂，但是灵活性很高，需要手动编写渲染的各个阶段，包括顶点处理和片元处理。在顶点处理阶段可以改变顶点的位置、颜色、法线等属性。而在片元处理阶段，可以处理像素级的操作，如纹理采样、光照计算、阴影等


# 二、顶点片元着色器写法

## 2.1 默认内容注释

首先是默认创建的Ulit Shader里的内容
```C#
Shader "Unlit/10.25" //定义一个名为"Unlit/10.25"的Shader
{
	Properties //定义Shader的属性
	{
		_MainTex ("Texture", 2D) = "white" {} //定义一个2D纹理属性，名为"_MainTex"，显示名称为"Texture"，默认值为白色
	}
	SubShader //定义子着色器
	{
		Tags { "RenderType"="Opaque" } //设置渲染类型为不透明
		LOD 100 //设置细节等级为100

		Pass //定义一个渲染通道
		{
			CGPROGRAM //开始编写CG程序
			#pragma vertex vert //指定顶点着色器函数为vert
			#pragma fragment frag //指定片元着色器函数为frag
			#pragma multi_compile_fog //启用雾效果
			
			#include "UnityCG.cginc" //包含Unity的CG库

			struct appdata //输入数据的结构体，包含顶点、法线、纹理坐标信息
			{
				float4 vertex : POSITION; //顶点位置
				float2 uv : TEXCOORD0; //纹理坐标
			};

			struct v2f //输出数据的结构体，包含顶点着色器计算后的数据，如变换后的位置、法线、纹理坐标
			{
				float2 uv : TEXCOORD0; //纹理坐标
				UNITY_FOG_COORDS(1) //雾效果坐标
				float4 vertex : SV_POSITION; //裁剪空间中的顶点位置
			};

			sampler2D _MainTex; //纹理采样器
			float4 _MainTex_ST; //纹理变换参数，ST是后缀，代表Scale缩放和Translation平移，_ST.xy存储了纹理的缩放值，_ST.zw存储了纹理的偏移值
			
			v2f vert (appdata v) //appdata_base 来自于UnityCG.cginc
			{
				v2f o;
				o.vertex = UnityObjectToClipPos(v.vertex); //将顶点位置从对象空间转换到裁剪空间
				o.uv = TRANSFORM_TEX(v.uv, _MainTex); //进行纹理坐标变换
				UNITY_TRANSFER_FOG(o,o.vertex); //传递雾效果数据
				return o;
			}
			
			fixed4 frag (v2f i) : SV_Target //片元着色器函数
			{
				fixed4 col = tex2D(_MainTex, i.uv); //采样纹理得到颜色值
				UNITY_APPLY_FOG(i.fogCoord, col); //应用雾效果
				return col;
			}
			ENDCG //结束CG程序编写
		}
	}
	FallBack "Diffuse" //如果当前硬件不支持该Shader，则使用内置的Diffuse Shader作为备选方案。
}

```

## 2.2 Tag标签
代码中的Tags是用于告知引擎何时以及用什么方式进行渲染的键值对参数，以下是常用的一些Tags
- Queue:指定渲染顺序，数值小的先渲染。预设的几个值包括
	-  Background(1000):最先调用，通常用于背景方面的渲染
	-  Geometry(2000):默认值，大多数物体用此方式渲染
	-  AlphaTest(2450):进行Alpha测试的几何体使用此队列
	- Transparent(3000):此渲染队列在Geometry和Alpha Test之后渲染，按照从后到前的顺序
	- Overlay(4000):此渲染队列是为了获得覆盖效果
- RenderType：用于将shader分组，以便选择合适的shader，预设的几个值包括
	- Opaque：默认值，表示不透明的物体，这些物体在渲染时不需要考虑深度排序或者混合等问题
	- Transparent：用于有透明效果的物体，这些物体渲染时需要考虑深度排序，以确保透明效果的正确
	- TransparentCutout：用于需要剔除像素而不是混合来实现透明效果的物体
	- Background：用于渲染背景
	- Overlay：用于渲染需要覆盖在所有物体之上的物体，如UI元素
- DisableBatching:使用绘制调用批处理时，一些着色器不起作用，这是因为批处理会将所有几何体转换为世界空间，所以对象空间会丢失，这时就可以使用DisableBatching标签来指示这种情况
- ForceNoShadowCasting：如果添加此标签并且值为true，则使用该子着色器渲染的对象不会投射阴影
- IgnoreProjector：如果添加此标签并且值为true，则使用此着色器的对象不会受到Projector（投影器组件）的影响
- PreviewType：指示材质检视面板预览如何显示材质。默认情况，材质显示为球体，但也可以将PreviewType设置为Plane或者Skybox
- CanUseSpriteAtlas：如果着色器用于精灵图，将此标签设置为false，这样在精灵打包到图集内时，此标签将不起作用


## 2.3 UnityCG定义的AppData结构体

```c#
struct appdata_base 
{
 float4 vertex : POSITION;//顶点坐标
 float3 normal : NORMAL;//顶点法线
 float4 texcoord : TEXCOORD0;//顶点UV
 UNITY_VERTEX_INPUT_INSTANCE_ID
};
struct appdata_tan 
{
 float4 vertex : POSITION;//顶点坐标
 float4 tangent : TANGENT;//顶点切线
 float3 normal : NORMAL;//顶点法线
 float4 texcoord : TEXCOORD0;//顶点UV
 UNITY_VERTEX_INPUT_INSTANCE_ID
};
struct appdata_full 
{
 float4 vertex : POSITION;//顶点坐标
 float4 tangent : TANGENT;//顶点切线
 float3 normal : NORMAL;//顶点法线
 float4 texcoord : TEXCOORD0;//顶点第一纹理坐标
 float4 texcoord1 : TEXCOORD1;//顶点第二纹理坐标
 float4 texcoord2 : TEXCOORD2;//顶点第三纹理坐标
 float4 texcoord3 : TEXCOORD3;//顶点第四纹理坐标
 fixed4 color : COLOR;//顶点颜色
 UNITY_VERTEX_INPUT_INSTANCE_ID
};
```
- appdata_base:是最基本的结构体，包含了位置（POSITION)、法线(NORMAL)和纹理坐标(TEXCOORD0)
- apptada_tan:这个结构体在appdata_base的基础上增加了切线(TANGENT)，通常用于需要进行法线贴图的Shader
- appdata_full:这个结构体包含了位置、法线、切线、顶点颜色（COLOR)以及两个纹理坐标。这是最完整的结构体，通常用于需要进行多种计算和操作的Shader
- appdata_img:这个结构体只包含位置和一个纹理坐标，通常用于只需要进行基本纹理映射的Shader

# 三、案例

## 3.1 将法线信息映射到颜色上

```c#
Shader "Unlit/10.25"
{
	Properties
	{
		
	}
	SubShader
	{
		Tags { "RenderType"="Opaque" }
		LOD 100

		Pass
		{
			CGPROGRAM
			#pragma vertex vert
			#pragma fragment frag
			#pragma multi_compile_fog
			
			#include "UnityCG.cginc"


			struct v2f
			{
				float4 vertex:SV_POSITION;
				float4 col:COLOR;
				float3 n:TEXCOORD0;
				float3 n2:TEXCOORD1;
			};

			
			v2f vert (appdata_base v)
			{
				v2f o;
				o.vertex = UnityObjectToClipPos(v.vertex);
				//将法线信息（从-1到1的向量）映射到颜色上（从0到1的向量）
				o.col.rgb = v.normal*0.5+0.5;
				o.n=fixed3(1,0,0);
				return o;
			}
			
			fixed4 frag (v2f i) : SV_Target
			{
				return i.col;
			}
			ENDCG
		}
	}
}
```

## 3.2 顶点片元着色器实现水波

```c#
Shader "Unlit/10.25" //定义一个名为"Unlit/10.25"的Shader
{
	Properties //定义Shader的属性
	{
		_MainTex("_MainTex",2D)="white"{} //定义一个2D纹理属性，名为"_MainTex"，默认值为白色
		_Color("Color",color)=(1,1,1,1) //定义一个颜色属性，名为"_Color"，默认值为白色

		_XSpeed("XSpeed",range(0,1))=0 //定义一个范围在0到1之间的浮点数属性，名为"_XSpeed"，默认值为0
		_YSpeed("YSpeed",range(0,1))=0 //定义一个范围在0到1之间的浮点数属性，名为"_YSpeed"，默认值为0
	}
	SubShader //定义子着色器
	{
		Tags { "RenderType"="Opaque" } //设置渲染类型为不透明
		LOD 100 //设置细节等级为100

		Pass //定义一个渲染通道
		{
			CGPROGRAM //开始编写CG程序
			#pragma vertex vert //指定顶点着色器函数为vert
			#pragma fragment frag //指定片元着色器函数为frag
			
			#include "UnityCG.cginc" //包含Unity的CG库

			sampler2D _MainTex; //纹理采样器
			float4 _MainTex_ST; //纹理变换参数
			fixed4 _Color; //颜色参数
			fixed _XSpeed; //X方向速度参数
			fixed _YSpeed; //Y方向速度参数

			struct v2f //定义顶点到片元的数据结构体
			{
				float4 vertex:SV_POSITION; //屏幕空间中的顶点位置
				float2 uv:TEXCOORD0; //纹理坐标
			};

			
			v2f vert (appdata_base v) //顶点着色器函数
			{
				v2f o;
				v.vertex.y+=0.2*sin((v.vertex.x+v.vertex.z)+_Time.y); //修改顶点的y坐标，实现顶点动画效果
				o.vertex = UnityObjectToClipPos(v.vertex); //将顶点位置从对象空间转换到裁剪空间
				o.uv=TRANSFORM_TEX(v.texcoord,_MainTex); //进行纹理坐标变换
				return o;
			}
			
			fixed4 frag (v2f i) : SV_Target //片元着色器函数
			{
				float2 uv=i.uv;
				uv+=float2(_XSpeed,_YSpeed)*_Time.y; //修改UV坐标，实现纹理滚动效果
				return _Color*tex2D(_MainTex,uv); //采样纹理并乘以颜色值，得到最终的颜色结果
			}
			ENDCG //结束CG程序编写
		}
	}
}

```

## 3.3 多次采样纹理实现星闪效果

```c#
Shader "Unlit/10.27Shader"
{
	Properties
	{
		_MainTex ("Texture", 2D) = "white" {}
		_StarTex("Star",2D)="white"{}
		_Color("Color",color)=(1,1,1,1)
	}
	SubShader
	{
		Tags { "RenderType"="Opaque" }
		LOD 100

		Pass
		{
			CGPROGRAM
			#pragma vertex vert
			#pragma fragment frag
			
			#include "UnityCG.cginc"

			sampler2D _MainTex;
			float4 _MainTex_ST;//_ST表示该纹理的缩放和平移信息
			sampler2D _StarTex;
			float4 _StarTex_ST;
			fixed4 _Color;

			//输入数据结构体
			struct appdata
			{
				float4 vertex : POSITION;	
				float3 normal : NORMAL;
				//纹理坐标
				float2 texcoord1:TEXCOORD0;
				float2 texcoord2:TEXCOORD1; 
			};

			//输出数据结构体
			struct v2f
			{
				//顶点在屏幕空间中的位置
				float4 vertex:SV_POSITION;
				float3 normal:NORMAL;
				float2 uv1:TEXCOORD0;
				float2 uv2:TEXCOORD1;
				float3 viewDir:TEXCOORD2;
			};


			
			v2f vert (appdata v)
			{
				v2f o;
				o.vertex = UnityObjectToClipPos(v.vertex);
				o.uv1=v.texcoord1;
				o.uv2=v.texcoord2;
				o.viewDir=normalize(UnityWorldSpaceViewDir(v.vertex.xyz));
				return o;
			}
			
			fixed4 frag (v2f i) : SV_Target
			{
				//采样Albedo
				fixed4 col=tex2D(_MainTex,TRANSFORM_TEX(i.uv1,_MainTex))*_Color;
				//第一次采样星光图
				fixed4 star01=tex2D(_StarTex,TRANSFORM_TEX(i.uv1,_StarTex));
				//第二次采样星光图，并且加上视线方向，保证视角在转动的时候，第二次采样的uv做偏移
				fixed4 star02=tex2D(_StarTex,TRANSFORM_TEX(i.uv1,_StarTex)+i.viewDir/5);
				//把两张星光图进行相乘，只有白色重合的地方才会显示，产生闪烁效果，最后乘星光颜色
				col.rgb+=(star01.rgb*star02.rgb);
				return col;
			}
			ENDCG
		}
	}
}

```

## 3.4 透明模式

```c#
Shader "Unlit/10.27Shader"
{
	Properties
	{
		_Color("MainColor",color)=(1,1,1,1)
		_MainTex("Base RGB",2D)="white"{}
	}
	SubShader
	{
		Tags { "RenderType"="Transparent" }

		Blend srcAlpha OneMinusSrcAlpha //正常透明混合
		//BlendOp Min Blend One One//变暗
		//Blend Max Blend One One //变凉
		//Blend DstColor zero //正片叠底
		//Blend OneMinusDstColor zero //滤色 柔和相加
		//Blend DstColor SrcColor //两倍相乘
		//Blend One One //减淡

		Pass
		{
			CGPROGRAM
			#pragma vertex vert
			#pragma fragment frag
			
			#include "UnityCG.cginc"

			sampler2D _MainTex;
			fixed4 _Color;

			//进行UV运算必须添加的变量
			float4 _MainTex_ST;

			//输入数据结构体
			struct appdata
			{
				float4 vertex:POSITION;
				float3 normal:NORMAL;
				float2 texcoord:TEXCOORD0;
			};

			//输出数据结构体
			struct v2f
			{
				float4 pos:POSITION;
				float2 texcoord:TEXCOORD0;
			};


			v2f vert (appdata v)
			{
				v2f o;
				//将顶点从模型空间转换到裁剪空间
				o.pos=UnityObjectToClipPos(v.vertex);
				//贴图的uv
				o.texcoord=TRANSFORM_TEX(v.texcoord,_MainTex);
				return o;
			}
			
			fixed4 frag (v2f i) : SV_Target
			{
				fixed4 col=tex2D(_MainTex,i.texcoord)*_Color;
				return col;
			}
			ENDCG
		}
	}
}
```

## 3.5 高斯模糊

```c#
Shader "Unlit/10.27Shader"
{
	Properties
	{
		_Radius("Radius",range(0,10))=0
		_MainTex("Base RGB",2D)="white"{}
	}
	SubShader
	{
		Pass
		{
			CGPROGRAM
			#pragma vertex vert
			#pragma fragment frag
			
			#include "UnityCG.cginc"

			//输出数据结构体
			struct v2f
			{
				float4 pos:POSITION;//顶点位置
				float2 uv:TEXCOORD0;//纹理坐标
				float2 uv1:TEXCOORD1;//周围纹理1
				float2 uv2:TEXCOORD2;//周围纹理2
				float2 uv3:TEXCOORD3;//周围纹理3
				float2 uv4:TEXCOORD4;//周围纹理4
			};

			//用到的变量
			sampler2D _MainTex;
			//xx_TexelSize,表示纹理的像素大小，width，height分别对应纹理的分辨率
			float4 _MainTex_TexelSize;
			//模糊半径
			float _Radius;

			v2f vert (appdata_base v)
			{
				v2f o;
				//将顶点从模型空间转换到裁剪空间
				o.pos=UnityObjectToClipPos(v.vertex);
				o.uv=v.texcoord.xy;
				//计算uv上下左右四个点对于blur半径下的uv坐标
				o.uv1=v.texcoord.xy+_Radius*_MainTex_TexelSize*float2(1,1);
				o.uv2=v.texcoord.xy+_Radius*_MainTex_TexelSize*float2(-1,1);
				o.uv3=v.texcoord.xy+_Radius*_MainTex_TexelSize*float2(-1,-1);
				o.uv4=v.texcoord.xy+_Radius*_MainTex_TexelSize*float2(1,-1);
				return o;
			}
			
			fixed4 frag (v2f i) : SV_Target
			{
				fixed4 color=fixed4(0,0,0,0);

				color+=tex2D(_MainTex,i.uv);
				color+=tex2D(_MainTex,i.uv1);
				color+=tex2D(_MainTex,i.uv2);
				color+=tex2D(_MainTex,i.uv3);
				color+=tex2D(_MainTex,i.uv4);
				//相加取平均值
				return color/5;
			}
			ENDCG
		}
	}
}
```

## 3.6. 通过多Pass绘制物体外轮廓
```c#
Shader "Unlit/NewUnlitShader"
{
 Properties
 {
 _MainTex ("Texture", 2D) = "white" {}
 _Outline ("Outline", Range(0, 1)) = 0.1
 _OutlineColor ("Outline Color", Color) = (1,1,1,1)
 }
 SubShader
 {
 Tags { "RenderType"="Opaque" }
 LOD 100
 Pass
 {
 CGPROGRAM
 #pragma vertex vert
 #pragma fragment frag
 
 sampler2D _MainTex;
 struct appdata
 {
 float4 vertex : POSITION;
 float2 uv : TEXCOORD0;
 };
 struct v2f
 {
 float2 uv : TEXCOORD0;
 float4 vertex : POSITION;
 };
 v2f vert (appdata v)
 {
 v2f o;
 o.vertex = UnityObjectToClipPos(v.vertex);
 o.uv = v.uv;
 return o;
 }
 fixed4 frag (v2f i) : SV_Target
 {
 return tex2D(_MainTex, i.uv);
 }
 ENDCG
 }
 Pass
 {
 //Cull Front是剔除正面
 //Cull Off 剔除关闭 默认Cull Back会剔除背面
 Cull Front
 CGPROGRAM
 #pragma vertex vert
 #pragma fragment frag
 float _Outline;
 float4 _OutlineColor;
 struct appdata
 {
 float4 vertex : POSITION;
 float3 normal : NORMAL;
 };
 struct v2f
 {
 float4 pos : POSITION;
 float3 normal:NORMAL;
 };
 v2f vert (appdata v)
 {
 v2f o;
 o.pos = UnityObjectToClipPos(v.vertex +
normalize(v.vertex) * _Outline);
 o.normal=v.normal;
 return o;
 }
 
 fixed4 frag (v2f i) : SV_Target
 { 
return fixed4((i.normal*0.5+0.5),1);//绘制基于法线的外轮廓
 //return _OutlineColor;//绘制固定颜色的外轮廓
 }
 ENDCG
 }
 }
}
```

## 3.7 双面渲染
```c#
Shader "Unlit/NewUnlitShader"
{
 Properties {
 _FrontColor ("Front Color", Color) = (1, 1, 1, 1)
 _BackColor ("Back Color", Color) = (1, 1, 1, 1)
 }
 SubShader {
 Tags { "Queue" = "Transparent" }
 LOD 200
 
 Pass
 {
 Blend SrcAlpha OneMinusSrcAlpha
 Cull Front
 CGPROGRAM 
 #pragma vertex vert
 #pragma fragment frag
 #include "UnityCG.cginc"
 float4 _BackColor;
 struct v2f
 { 
 float4 vertex : POSITION;
 };
 v2f vert(appdata_base i)
 { 
 v2f o;
 o.vertex=UnityObjectToClipPos(i.vertex); 
 return o;
 }
 fixed4 frag(v2f i) : SV_Target
 { 
 return _BackColor;
 }
 ENDCG
 }
 Pass
 {
 Blend SrcAlpha OneMinusSrcAlpha
 Cull Back
 CGPROGRAM 
 #pragma vertex vert
 #pragma fragment frag
 #include "UnityCG.cginc"
 float4 _FrontColor;
 struct v2f
 { 
 float4 vertex : POSITION;
 };
 v2f vert(appdata_base i)
 { 
 v2f o;
 o.vertex=UnityObjectToClipPos(i.vertex); 
 return o;
 }
 fixed4 frag(v2f i) : SV_Target
 { 
 return _FrontColor;
 }
 ENDCG
 }
 }
}

```

## 3.8 流光效果
```c#
Shader "Unlit/NewUnlitShader"
{
 Properties
 {
 _MainTex ("Texture", 2D) = "white" {}
 _LightTex("LightTex",2D)="white"{}
 _Speed("Speed",range(0,100))=0
 }
 SubShader
 {
 Tags { "Queue"="Transparent" }
 LOD 100
 Pass
 {
 Blend srcAlpha OneMinusSrcAlpha
 CGPROGRAM
 #pragma vertex vert
 #pragma fragment frag
 #include "UnityCG.cginc"
 sampler2D _MainTex;
 sampler2D _LightTex;
 float4 _LightTex_ST;
 float _Speed;
 struct v2f
 {
 float2 uv : TEXCOORD0;
 float2 uv1 : TEXCOORD1;
 float4 vertex : POSITION;
 };
 v2f vert (appdata_base v)
 {
 v2f o;
 o.vertex = UnityObjectToClipPos(v.vertex);
 o.uv = v.texcoord;
 o.uv1= TRANSFORM_TEX(v.texcoord,_LightTex);
 o.uv1.x+=_Time.y*_Speed; 
 return o;
 }
 
 fixed4 frag (v2f i) : SV_Target
 {
 return tex2D(_MainTex,
i.uv)+tex2D(_LightTex,i.uv1)*0.5;
 }
 ENDCG
 }
 }
}
```

## 3.9物体消融效果
```c#
Shader "Unlit/NewUnlitShader 1"
{
 Properties
 {
 _MainTex ("Texture", 2D) = "white" {}
 _Speed ("Burn Speed", Range(0,1)) = 0.1
 }
 SubShader
 {
 Pass
 {
 Cull Off
 CGPROGRAM
 #pragma vertex vert
 #pragma fragment frag
 #include "UnityCG.cginc"
 sampler2D _MainTex;
 float4 _MainTex_ST;
 fixed _Speed;
 
 fixed _Amount; 
 struct appdata{
 float4 vertex : POSITION;
 float4 uv : TEXCOORD0;
 };
 struct v2f{
 float4 pos : SV_POSITION;
 float2 uv : TEXCOORD0;
 };
 v2f vert (appdata v)
 {
 v2f o;
 o.pos = UnityObjectToClipPos(v.vertex);
 o.uv = TRANSFORM_TEX(v.uv, _MainTex);
 
 return o;
 }
 
 fixed4 frag (v2f i) : SV_Target
 {
 fixed3 col = tex2D(_MainTex, i.uv).rgb;
 _Amount += _Time.y * _Speed;
 //透明度测试
 //equals to:
 //if(col.r - _Amount < 0)
 // discard;
 clip(col.r - _Amount); 
 
 return fixed4(col, 1);
 }
 ENDCG
 }
 }
 }
```

## 3.10 白炽灯效果
```c#
Shader "Unlit/NewUnlitShader 2"
{
 Properties
 {
 _MainTex ("Texture", 2D) = "white" {}
 _Color ("EdgeColor", Color) = (1, 0, 0, 1)
 //发光强度
 _Power ("EdgePower", Range(-2, 2)) = -2
 //最小发光值
 _EdgeMin("Min", Range(0, 1)) = 0.5
 }
 SubShader
 {
 Tags { "Queue" = "Transparent" }
 Blend SrcAlpha OneMinusSrcAlpha
 Pass
 {
 CGPROGRAM
 #pragma vertex vert
 #pragma fragment frag
 #include "UnityCG.cginc"
 struct appdata
 {
 float4 vertex : POSITION;
 float2 uv : TEXCOORD0;
 float3 normal : NORMAL;
 };
 struct v2f
 { 
 float4 vertex : SV_POSITION;
 float2 uv : TEXCOORD0;
 float3 worldNormal : TEXCOORD1;
 float3 worldViewDir : TEXCOORD2;
 };
 sampler2D _MainTex;
 float4 _MainTex_ST;
 fixed4 _Color;
 half _Power;
 fixed _EdgeMin;
 v2f vert (appdata v)
 {
 v2f o;
 //v.vertex.xyz += v.normal * 0.1;
 o.vertex = UnityObjectToClipPos(v.vertex);
 o.uv = TRANSFORM_TEX(v.uv, _MainTex);
 o.worldNormal = UnityObjectToWorldNormal(v.normal);
 o.worldViewDir = WorldSpaceViewDir(v.vertex);
 return o;
 }
 fixed4 frag (v2f i) : SV_Target
 {
 fixed4 col = tex2D(_MainTex, i.uv);
 float dotv = saturate(dot(normalize(i.worldNormal),
normalize(i.worldViewDir)));
 float edge = 1 - dotv;
 if(edge > _EdgeMin)
 {
 fixed4 finalCol = pow(dotv, _Power) * _Color * 2;
 finalCol.a = pow(dotv,_Power);
 return finalCol;
 }
 else
 return col;
 }
 ENDCG
 }
 }
}

```

## 3.11 扫描显示效果
```c#
Shader "Unlit/NewUnlitShader 2"
{
 Properties
 {
 _MainTex ("MainTex", 2D) = "white" {}
 _MeshTex ("MeshTex", 2D) = "white" {}
 _Speed ("Speed", Range(0.1, 1)) = 0.5
 }
 SubShader
 {
 Tags { "Queue" = "Transparent" "RenderType"="Transparent" }
 Pass
 {
 ZWrite off
 Blend SrcAlpha OneMinusSrcAlpha
 CGPROGRAM
 #pragma vertex vert
 #pragma fragment frag
 #include "UnityCG.cginc"
 struct appdata
{
 float4 vertex : POSITION;
 float2 uv : TEXCOORD0;
 };
 struct v2f
 {
 float4 vertex : SV_POSITION;
 float2 uv : TEXCOORD0;
 };
 sampler2D _MainTex;
 float4 _MainTex_ST;
 sampler2D _MeshTex;
 half _Speed;
 v2f vert (appdata v)
 {
 v2f o;
 o.vertex = UnityObjectToClipPos(v.vertex);
 o.uv = TRANSFORM_TEX(v.uv, _MainTex);
 return o;
 }
 
 fixed4 frag (v2f i) : SV_Target
 {
 half y = i.vertex.y;
 half threY = lerp(800,0,_Time.y*_Speed);
 if(y > threY)
 {
 fixed4 col = tex2D(_MainTex, i.uv);
 return col;
 }
 else
 {
 return fixed4(0, 0, 0, 0);
 }
 }
 ENDCG
 }
 }
```


```c#
Shader "Unlit/NewUnlitShader"
{
 Properties
 {
 _MainTex("MainTex",2D)="white"{}
 _Speed("Speed",range(0,3))=1
 _Width("Width",range(0,1))=0
 _LightColor("LightColor",color)=(1,1,1,1)
 _Angle("Angle",range(-1,1))=0
 }
 SubShader
 {
 Pass
 {
 CGPROGRAM
 #pragma vertex vert
 #pragma fragment frag
 #include "UnityCG.cginc"
 sampler2D _MainTex;
 float4 _MainTex_ST;
 float _Speed;
 fixed _Width;
 fixed4 _LightColor;
 half _Angle;
 struct appdata
 {
 float4 vertex:POSITION;
 float2 uv:TEXCOORD0;
 };
 struct v2f
 {
 float4 vertex : SV_POSITION;
 float2 uv:TEXCOORD;//纹理坐标
 };
 v2f vert (appdata v)
 {
 v2f o;
 o.vertex = UnityObjectToClipPos(v.vertex);
 o.uv=TRANSFORM_TEX(v.uv,_MainTex);
 return o;
 }
 fixed4 frag (v2f i) : SV_Target
 { 
 fixed4 col=tex2D(_MainTex,i.uv);
 float offset=i.uv.x+i.uv.y*_Angle;
 float v=sin(offset+_Time.y*_Speed);
 //smoothstep(a,b,x) 用来生成0-1的平滑过度值
 //x<a<b 或 x>a>b 返回0
 //x<b<a 或 x>b>a 返回1
 v=smoothstep(1-_Width,1.0,v);
 float3 target=float3(v,v,v)*_LightColor.rgb+col.rgb;
 return fixed4(target,col.a);
 }
 ENDCG
 }
 }
}
```

# 四、颜色混合的原理

## 1.正片叠底

即两个颜色的色值相乘。由于黑色色值是（0，0，0，0），白色色值是（1，1，1，1）。

因此黑色和任何颜色混合都是黑色
（1,1,1,1）*（0,0,0,0）=（0,0,0,0）
白色和任何其他颜色混合都得其他颜色
(1,1,1,1)*(0.5,0.5,0.5,0.5)=(0.5,0.5,0.5,0.5)


## 2.线性叠加

用于将两个或多个颜色值相加以产生最终的颜色值。线性叠加的计算方式是将每个颜色通道（如红色、绿色和蓝色）的值相加，当颜色叠加的值超过1时会产生发光效果

```c#
Shader "Unlit/10.25"
{
	Properties
	{
		_MainTex("_MainTex",2D)="white"{}
	}
	SubShader
	{
		Tags { "RenderType"="Opaque" }

		Pass
		{
			CGPROGRAM
			#pragma vertex vert
			#pragma fragment frag
			
			#include "UnityCG.cginc"

			sampler2D _MainTex;
			float4 _MainTex_ST;

			struct v2f
			{
				float4 vertex:SV_POSITION;
				float2 uv:TEXCOORD0;
			};

			
			v2f vert (appdata_base v)
			{
				v2f o;
				o.vertex = UnityObjectToClipPos(v.vertex);
				o.uv=TRANSFORM_TEX(v.texcoord,_MainTex);
				return o;
			}
			
			fixed4 frag (v2f i) : SV_Target
			{
				float l=cos(_Time.y*10)+2;
				return tex2D(_MainTex,i.uv)*l;
			}
			ENDCG
		}
	}
}

```